plugins {
  id 'application'
}

dependencies {
  implementation project(':packages:dictionary-runtime')

  testImplementation(platform(libs.junit.bom))
  testImplementation(libs.assertj.core)
  testImplementation(libs.junit.jupiter)
  testRuntimeOnly(libs.junit.platform.launcher)
}

application {
  mainClass = 'com.scrabble.dictionary.tools.DictionaryCli'
}

tasks.register('compileOsps', JavaExec) {
  group = 'dictionary'
  description = 'Compile OSPS word list into FST + metadata.'

  classpath = sourceSets.main.runtimeClasspath
  mainClass = application.mainClass

  def inputPath = project.findProperty('ospsInput') ?: 'osps_shortened.txt'
  def outputPath = project.findProperty('ospsOutput') ?: 'artifacts/osps.fst'

  def resolvedInput = rootProject.file(inputPath).absolutePath
  def resolvedOutput = rootProject.file(outputPath).absolutePath

  args 'compile', '--input', resolvedInput, '--output', resolvedOutput
}
