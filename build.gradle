allprojects {
  group = 'com.scrabble'
  version = '0.1.0-SNAPSHOT'
}

subprojects {
  repositories {
    mavenCentral()
  }
}

subprojects {
  plugins.withType(JavaBasePlugin).configureEach {
    java {
      sourceCompatibility = JavaVersion.VERSION_25
      targetCompatibility = JavaVersion.VERSION_25
    }
  }

  tasks.withType(JavaCompile).configureEach {
    options.release = 25
  }

  tasks.withType(Test).configureEach {
    useJUnitPlatform()
  }
}

subprojects {
  pluginManager.withPlugin('java') {
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'

    checkstyle {
      toolVersion = '12.3.0'
      configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    }

    tasks.withType(Checkstyle).configureEach {
      reports {
        xml.required = false
        html.required = true
      }
    }

    jacoco {
      toolVersion = '0.8.14'
    }

    tasks.named('jacocoTestCoverageVerification').configure {
      violationRules {
        rule {
          limit {
            counter = 'LINE'
            value = 'COVEREDRATIO'
            minimum = 0.75
          }
        }
      }
    }

    tasks.named('jacocoTestReport', JacocoReport).configure {
      dependsOn tasks.named('test')
      reports {
        xml.required = true
        html.required = true
      }
    }

    tasks.named('check').configure {
      dependsOn tasks.named('jacocoTestCoverageVerification')
    }
  }
}
